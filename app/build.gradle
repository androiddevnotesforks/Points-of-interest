plugins {
    id 'kotlin-kapt'
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
}

def CREDENTIALS_DEBUG = '../keystore/credentials-debug.properties'
def CREDENTIALS_RELEASE = '../keystore/credentials-release.properties'

def STORE_FILE = 'keystore.file'
def STORE_PASSWORD = 'keystore.password'
def KEY_ALIAS = 'key.alias'
def KEY_PASSWORD = 'key.password'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.grishko188.pointofinterest"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {

        def credentialsDebug = new Properties()
        file(CREDENTIALS_DEBUG).withInputStream {
            credentialsDebug.load(it)
        }

        debug {
            storeFile file(credentialsDebug[STORE_FILE])
            storePassword credentialsDebug[STORE_PASSWORD]
            keyAlias credentialsDebug[KEY_ALIAS]
            keyPassword credentialsDebug[KEY_PASSWORD]
        }

        release {
            def credentialsRelease = new Properties()
            def credentialsReleaseFile = file(CREDENTIALS_RELEASE)
            if (credentialsReleaseFile.exists()) {
                credentialsReleaseFile.withInputStream { credentialsRelease.load(it) }
                println("credentials-release.properties is exist")
            } else {
                println("credentials-release.properties isn't exist - credentials-debug.properties will be used")
                credentialsRelease = credentialsDebug
            }

            storeFile file(credentialsRelease[STORE_FILE])
            storePassword credentialsRelease[STORE_PASSWORD]
            keyAlias credentialsRelease[KEY_ALIAS]
            keyPassword credentialsRelease[KEY_PASSWORD]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
        allWarningsAsErrors = false
        freeCompilerArgs += [
                '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api'
        ]
    }
    kapt {
        correctErrorTypes = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.grishko188.pointofinterest'
}

dependencies {

    implementation(project(":data"))
    implementation(project(":domain"))

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.1'
    implementation("androidx.compose.material:material:1.3.1")
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation("androidx.navigation:navigation-compose:2.5.3")
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation("androidx.core:core-splashscreen:1.0.0")

    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation("io.coil-kt:coil-compose:2.2.2")
    implementation "com.google.accompanist:accompanist-flowlayout:0.28.0"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation 'com.google.android.gms:play-services-auth:20.4.0'
    implementation "androidx.browser:browser:1.4.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}